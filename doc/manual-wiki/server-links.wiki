
=Creating links and forms=

<<outline depth="2"| <<header| **Table of contents** >> >>

==@@id="links"@@Links

To create a link ({{{<a>}}}), use the
 <<a_api| val Eliom_content.Html.D.a >> function,
as in these examples:

<<code language="ocaml"|
 open Eliom_content

 let links = Eliom_registration.Html.create
   ~path:(Eliom_service.Path ["rep";"links"])
   ~meth:(Eliom_service.Get Eliom_parameter.unit)
 (fun () () ->
   Lwt.return
    Html.D.(
      html
       (head (title (pcdata "Links")) [])
       (body
         [p
          [a theservice [pcdata "coucou"] (); br ();
           a hello [pcdata "hello"] (); br ();
           a default
             [pcdata "default page of the dir"] (); br ();
           a uasuffix
             [pcdata "uasuffix"] (2007,06); br ();
           a service_with_params
             [pcdata "Service with params"] (42,(22,"ciao")); br ();
           a raw_serv
             [pcdata "raw_serv"] [("sun","yellow");("sea","blue and pink")]; br ();
           a
             (Eliom_service.Http.extern
                ~prefix:"http://fr.wikipedia.org"
                ~path:["wiki";""]
                ~meth:(Eliom_service.Get (suffix (all_suffix "suff")))
                ())
             [pcdata "OCaml on wikipedia"]
             ["OCaml"]; br ();
           Raw.a
             ~a:[a_href (Raw.uri_of_string "http://en.wikipedia.org/wiki/OCaml")]
             [pcdata "OCaml on wikipedia"]
         ]])))
>>

<<a_api| val Eliom_content.Html.D.a >> takes as first parameter
the service you want to link to.
The second parameter is the text of the link.
The last parameter is for
GET parameters you want to put in the link.
The type of this parameter and the name of GET parameters depend
on the service you link to.


The link to Wikipedia shows how to define an external service (here it
uses a suffix URL).
For an external service without parameters, you can use the low level
function  <<a_api| val Eliom_content.Html.D.Raw.a >>, if you don't want to create an
external service explicitly.
Note that the path must be a list of strings:
do not write {{{["foo/bar"]}}},
but {{{["foo";"bar"]}}}; otherwise, the "/" will be encoded in
the URL.\\


If you want to create (mutually or not) recursive pages, create the
service using <<a_api| val Eliom_service.create >> first, then
register it in the table using (for example) <<a_api| val Eliom_registration.Html.register >>:

<<code language="ocaml"|
let linkrec =
  Eliom_service.create
    ~path:(Eliom_service.Path ["linkrec"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit) ()

let _ =
  Eliom_registration.Html.register
    linkrec
    (fun () () ->
       Lwt.return
         (html
            (head (title (pcdata "")) [])
            (body [p [a linkrec [pcdata "click"] ()]])))
>>

(You can also refer to //the current service// as <<a_api
project="eliom"|val Eliom_service.reload_action>>.)

==@@id="forms"@@Forms

===Forms towards GET services

The modules <<a_api| val Eliom_content.Html.D.Form >>
and <<a_api| val Eliom_content.Html.F.Form >> provide functions
for creating forms. Our examples use shorthands to these modules, e.g.,
simply <<span class="code"|Form>>, assuming an appropriate context.

The function <<a_api| val Eliom_content.Html.D.Form.get_form >>
allows creation of forms that use the GET method (parameters in the
URL). It works like <<a_api| val Eliom_content.Html.D.a >> but takes
a //function// that creates the form from the parameters names as
parameter.

<<code language="ocaml"|
let create_form =
  (fun (number_name, (number2_name, string_name)) ->
    Html.D.(
      [p [pcdata "Write an int: ";
        Form.input ~input_type:`Text ~name:number_name Form.int;
        pcdata "Write another int: ";
        Form.input ~input_type:`Text ~name:number2_name Form.int;
        pcdata "Write a string: ";
        Form.input ~input_type:`Text ~name:string_name Form.string;
        Form.input ~input_type:`Submit ~value:"Click" Form.string]]
    ))

let form =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["form"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
       let f = Html.D.Form.get_form service_with_params create_form in
       Lwt.return
         (html
           (head (title (pcdata "")) [])
           (body [f])))
>>

Parameter names are typed to make sure that they are used
properly. The form-creating functions in <<span
class="code"|Eliom_content.Html.D.Form>> (respectively <<span
class="code"|Eliom_content.Html.F.Form>>) accept an argument of type
<<a_api| type Eliom_content.Html.D.Form.param >> (respectively
<<a_api| type Eliom_content.Html.F.Form.param >>), which needs to
match the type of the parameter used. For example,
<<span class="code"|number_name>> has type <<span
class="code"|int param_name>>, so <<span class="code"|Form.int>>
must be used with <<span class="code"|Form.input>>
(or with the other widgets), whereas
<<span class="code"|string_name>> has type
<<span class="code"|string param_name>>
and must be used in conjunction with the argument
<<span class="code"|Form.string>>.
Creating form widgets are described in detail in
<<a_api | module Eliom_content.Html.D.Form >>
(and <<a_api | module Eliom_content.Html.F.Form >>).

For untyped forms, you may use functions from the module <<a_api
project="eliom" subproject="server"|module
Eliom_content.Html.D.Raw>>.  Here is a form linking to our (untyped)
service <<span class="code"|raw_serv>>.

<<code language="ocaml"|
let raw_form =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["anyform"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
      Lwt.return
        Html.D.(html
                   (head (title (pcdata "")) [])
                   (body
                      [h1 [pcdata "Any Form"];
                       Form.get_form raw_serv
                         (fun () ->
                           [p [pcdata "Form to raw_serv: ";
                               raw_input ~input_type:`Text ~name:"plop" ();
                               raw_input ~input_type:`Text ~name:"plip" ();
                               raw_input ~input_type:`Text ~name:"plap" ();
                               raw_input ~input_type:`Submit ~value:"Click" ()]])
                        ])))
>>

===POST parameters


By default web page parameters are transferred in the URL (GET parameters).
A web page may also expect POST parameters
(that is, parameters that are not in the URL but in the body of the HTTP
request, if the POST method is used).

<<code language="ocaml"|let no_post_param_service =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["post"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
      Lwt.return
        (html
         (head (title (pcdata "")) [])
         (body [h1 [pcdata
                      "Version of the page without POST parameters"]])))

let my_service_with_post_params =
  Eliom_registration.Html.attach_post
    ~fallback:no_post_param_service
    ~post_params:Eliom_parameter.(string "value")
    (fun () value ->
      Lwt.return
        (html
         (head (title (pcdata "")) [])
         (body [h1 [pcdata value]])))
>>




Services may take both GET and POST parameters:


<<code language="ocaml"|let get_no_post_param_service =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["post2"])
    ~meth:(Eliom_service.Get (Eliom_parameter.int "i"))
    (fun i () ->
      Lwt.return
        (html
         (head (title (pcdata "")) [])
         (body [p [pcdata "No POST parameter, i:";
                   em [pcdata (string_of_int i)]]])))

let my_service_with_get_and_post =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["post2"])
    ~meth:(Eliom_service.Post (Eliom_parameter.int "i",
                               Eliom_parameter.string "value"))
    (fun i value ->
      Lwt.return
        (html
           (head (title (pcdata "")) [])
           (body [p [pcdata "Value: ";
                     em [pcdata value];
                     pcdata ", i: ";
                     em [pcdata (string_of_int i)]]])))
>>

===@@id="postforms"@@POST forms


To create a POST form, use the
<<a_api| val Eliom_content.Html.D.Form.post_form >> function.
It is similar to <<a_api| val Eliom_content.Html.D.Form.get_form >>
with an additional parameter for the GET parameters you want to put in
the URL (if any). Here, <<span class="code"|form2>> is a page containing
a form to the service <<span class="code"|post>> (using Html.F's
functions) and <<span class="code"|form3>> (defined using the syntax
contains a form to <<span class="code"|post2>>, with a GET parameter.
<<span class="code"|form4>> is a form to an external page.

//Warning:// Some examples in this section use a HTML syntax extension
which is only provided for Camlp4, not for PPX. We recommend that you
use the modules
<<a_api subproject="server" | module Eliom_content.Html.D >> and
<<a_api subproject="server" text="F"| module Eliom_content.Html.F >>
to produce HTML content.

<<code language="ocaml"|
  let form2 =
    Eliom_registration.Html.create
      ~path:(Eliom_service.Path ["form2"])
      ~meth:(Eliom_service.Get Eliom_parameter.unit)
      (fun () () ->
         let f =
           (Eliom_content.Html.D.Form.post_form my_service_with_post_params
              (fun chaine ->
                 [p [pcdata "Write a string: ";
                  Form.input
                    ~input_type:`Text ~name:chaine
                    Form.string]]) ())
         in
         Lwt.return
           (html
             (head (title (pcdata "form")) [])
             (body [f])))

let form3 =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["form3"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
       let module Html = Html.D in
       let f  =
         (Eliom_content.Html.D.Form.post_form my_service_with_get_and_post
            (fun chaine ->
              <:html5list< <p> Write a string:
                      $Form.input
                      ~input_type:`Text ~name:chaine
                       Form.string$ </p> ~>>)
            222) in
       Lwt.return
         << <html>
              <head><title></title></head>
              <body>$f$</body></html> ~>>)

let form4 =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["form4"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
  (fun () () ->
     let module Html = Eliom_content.Html.D in
     let f  =
       (Eliom_content.Html.D.Form.post_form
          (extern
             ~prefix:"http://www.petizomverts.com"
             ~path:["zebulon"]
             ~meth:(Eliom_service.Post (int "i", string "chaine"))
             ())
          (fun chaine ->
            <:html5list< <p> Write a string:
                     $Form.input ~input_type:`Text ~name:chaine
                        Form.string$ </p> ~>>)
          222) in
     Lwt.return
       (html
        (head (title (pcdata "form")) [])
        (body [f])))
>>

==@@id="advancedformsandparameters"@@Advanced forms and parameters

This section shows more advanced use of page parameters and
corresponding forms.

===Parsing parameters using regular expressions


Eliom_parameter.regexp allows parsing page parameters using (Perl-compatible)
regular expressions. We use the module <<span class="code"|Netstring_pcre>>,
from //OCamlnet//. See the documentation about OCamlnet
for more information.
The following example shows a service that accepts only parameters
values enclosed between <<span class="code"|[>> and <<span class="code"|]>>:

<<code language="ocaml"|
let r = Netstring_pcre.regexp "\\\\[(.*)\\\\]"

let regexp =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["regexp"])
    ~meth:
      (Eliom_service.Get
        Eliom_parameter.(regexp r "$1" (fun s -> s) "myparam"))
    (fun g () ->
      Lwt.return
        Html.D.(html
                  (head (title (pcdata "")) [])
                  (body [p [pcdata g]])))

>>

<<code language="ocaml"|let myregexp = Netstring_pcre.regexp "\\[(.*)\\]"

let regexpserv =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["regexp"])
    ~meth:(Eliom_service.Get
            Eliom_parameter.(regexp myregexp "$1" (fun s -> s) "myparam"))
    (fun g () ->
      Lwt.return
        Html.D.(html
                  (head (title (pcdata "")) [])
                  (body [p [pcdata g]])))
>>

===Boolean checkboxes

Page may take parameter of type <<span class="code"|bool>>.
A possible use of this type is in a form
with //boolean checkboxes//, as in the example below:

<<code language="ocaml"|(* Form with bool checkbox: *)
let bool_params =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["bool"])
    ~meth:(Eliom_service.Get Eliom_parameter.(bool "case"))
  (fun case () ->
    let module Html = Html.D in
    Lwt.return
      << <html>
           <head><title></title></head>
           <body>
           <p>
             $pcdata (if case then "checked" else "not checked")$
           </p>
           </body>
         </html> ~>>)

let create_form_bool casename =
    let module Html = Html.D in
    <:html5list< <p>check? $bool_checkbox ~name:casename ()$ <br/>
      $Form.input ~input_type:`Submit ~value:"Click" Form.string$</p> ~>>

let form_bool =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["formbool"])
    ~meth:(Eliom_parameter.Get unit)
  (fun () () ->
     let module Html = Html.D in
     let f = Form.get_form bool_params create_form_bool in
     Lwt.return
       << <html>
            <head><title></title></head>
            <body> $f$ </body>
          </html> ~>>)


>>

//Important warning://
As you can see, browsers do not send any value
for unchecked boxes! An unchecked box is equivalent to no parameter
at all! Thus it is not possible to distinguish between a service
taking a boolean and a service taking no parameter at all
(if they share the same URL).
In Eliom services with higher priority are tried first, and then
they are tried in order of registration.
The first matching service will answer.\\

Other types similar to bool:
* <<a_api| val Eliom_parameter.opt >> (page taking an optional parameter),
* <<a_api| val Eliom_parameter.sum >> (either a parameter or another).

See <<a_api | module Eliom_parameter>>.

===Type <<span class="code"|set>>

Page may take several parameters of the same name.
It is useful when you want to create a form with a variable number
of fields.
To do that with Eliom, use the type  <<a_api| val Eliom_parameter.set >>.
For example, <<span class="code"|set int "val">> means that the page will take
zero, one, or several parameters of name <<span class="code"|"val">>,
all of type <<span class="code"|int>>.
The function you register will receive the parameters in a list.
Example:

<<code language="ocaml"|let set = Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["set"])
    ~meth:(Eliom_service.Get Eliom_parameter.(set string "s"))
  (fun l () ->
    let module Html = Html.D in
    let ll =
      List.map
        (fun s -> << <strong>$str:s$ </strong> ~>>) l
    in
    Lwt.return
    << <html>
         <head><title></title></head>
         <body>
         <p>
           You sent:
           $list:ll$
         </p>
         </body>
       </html> ~>>)
>>

These parameters may come from several kinds of widgets in forms.
Here is an example of a form with several checkboxes, all sharing the
same name, but with different values:

<<code language="ocaml"|
(* form to set *)
let setform = Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["setform"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
      Lwt.return
        (html
           (head (title (pcdata "")) [])
           (body [h1 [pcdata "Set Form"];
                  Form.get_form set
                    (fun n ->
                      [p [pcdata "Form to set: ";
                          Form.checkbox
                            ~name:n ~value:"box1"
                            Form.string;
                          Form.checkbox
                            ~name:n ~value:"box2" ~checked:true
                            Form.string;
                          Form.checkbox
                            ~name:n ~value:"box3"
                            Form.string;
                          Form.checkbox
                            ~name:n ~value:"box4"
                            Form.string;
                          Form.input
                            ~input_type:`Submit ~value:"Click"
                            Form.string]])
                ])))
>>

Once again, note that there is no difference between an empty
set or no parameter at all. If you register a service without parameters
and a service with a set of parameters on the same URL, the service with
higher priority, or the firstly registered service that matches,
will answer.



===Select


Here is an example of a select box.

<<code language="ocaml"|let select_example_result = Eliom_registration.Html.creeate
    ~path:(Eliom_service.Path ["select"])
    ~meth:(Eliom_service.Get Eliom_parameter.(string "s"))
    (fun g () ->
      Lwt.return
        (html
           (head (title (pcdata "")) [])
           (body [p [pcdata "You selected: ";
                     strong [pcdata g]]])))

let create_select_form =
  (fun select_name ->
    Html.D.(
      [p [pcdata "Select something: ";
        Form.select Form.string ~name:select_name
          (Form.Option ([] (* attributes *),
                        "Bob" (* value *),
                        None (* Content, if different from value *),
                        false (* not selected *))) (* first line *)
          [Form.Option ([], "Marc", None, false);
          (Form.Optgroup
          ([],
           "Girls",
           ([], "Karin", None, false),
           [([a_disabled `Disabled], "Juliette", None, false);
            ([], "Alice", None, true);
            ([], "Germaine", Some (pcdata "Bob's mother"), false)]))]
          ;
        Form.input ~input_type:`Submit ~value:"Send" Form.string]]
    ))

let select_example =
  Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["select"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
  (fun () () ->
     let open Html.D in
     let f = Form.get_form select_example_result create_select_form in
     Lwt.return
       (html
         (head (title (pcdata "")) [])
         (body [f])))
>>

To do "multiple" select boxes, use functions like
 <<a_api| val Eliom_content.Html.D.Form.multiple_select >>.
As you can see in the type, the service must be declared with parameters
of type  <<a_api| val Eliom_parameter.set >>.

===Clickable images


Here is an example of clickable image.
      You receive the coordinates the user clicked on.

<<code language="ocaml"|
  let coord = Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["coord"])
    ~meth:(Eliom_service.Get Eliom_parameter.(coordinates "coord"))
  (fun c () ->
    let module Html = Html.D in
    Lwt.return
      << <html>
           <head><title></title></head>
           <body>
           <p>
             You clicked on coordinates:
             ($str:(string_of_int c.abscissa)$, $str:(string_of_int c.ordinate)$)
           </p>
           </body>
         </html> ~>>)

(* form to image *)
let imageform = Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["imageform"])
    ~meth:(Eliom_service.Get Eliom_parameter.unit)
    (fun () () ->
      Lwt.return
        (html
           (head (title (pcdata "")) [])
           (body [h1 [pcdata "Image Form"];
                  Form.get_form coord
                    (fun n ->
                      [p [Form.image_input
                            ~src:(make_uri ~service:(Eliom_service.static_dir ()) ["ocsigen5.png"])
                            ~name:n
                            ()]])
                ])))
>>

===Type <<span class="code"|list>>

Another way (than  <<a_api| val Eliom_parameter.set >>) to do variable length forms
is to use indexed lists (using  <<a_api| val Eliom_parameter.list >>).
The use of that feature is a bit more complex than <<span class="code"|set>>.
Here is an example of service taking an indexed list as parameter:

<<code language="ocaml"|
(* lists *)
let service_list = Eliom_registration.Html.create
    ~path:(Eliom_service.Path ["thepath"])
    ~meth:(Eliom_service.Get Eliom_parameter.(list "a" (string "str")))
  (fun l () ->
    let module Html = Html.D in
    let ll =
      List.map (fun s -> << <strong>$str:s$</strong> ~>>) l in
      Lwt.return
        << <html>
             <head><title></title></head>
             <body>
             <p>
               You sent:
               $list:ll$
             </p>
             </body>
           </html> ~>>)
>>

//Warning://
As for sets or bools,
if a request has no parameter, it will be considered as the empty list.
Services with higher priority are tried first, otherwise
they are tried in order of registration.

As you see, the names of each list element is built from the name
of the list, the name of the list element, and an index.
To spare you creating yourself these names, Eliom provides you an iterator
to create them.

<<code language="ocaml"|(* Form with list: *)
let create_listform f =
  (* Here, f.it is an iterator like List.map,
     but it must be applied to a function taking 3 arguments
     (unlike 1 in map), the first one being the name of the parameter,
     and the second one the element of list.
     The last parameter of f.it is the code that must be appended at the
     end of the list created
   *)
  let module Html = Eliom_content.Html.D in
  f.it (fun stringname v init ->
    <:html5list< <p>Write the value for $str:v$:
      $Form.input ~input_type:`Text ~name:stringname Form.string$ </p> ~>>@init)
    ["one";"two";"three";"four"]
    <:html5list< <p>$Form.input ~input_type:`Submit ~value:"Click" Form.string$</p> ~>>

let listform = Eliom_registration.Html.create
  ~path:(Eliom_service.Path ["listform"])
  ~meth:(Eliom_service.Get Eliom_parameter.unit)
  (fun () () ->
     let f = Form.get_form service_list create_listform in
     let module Html = Eliom_content.Html.D in
     Lwt.return
       << <html>
            <head><title></title></head>
            <body> $f$ </body>
          </html> ~>>)

>>

//Important warning://
As we have seen in the section about boolean (or optional)
parameters, it is not possible to distinguish between a boolean
with value "false", and no parameter at all.
This causes problems if you create a list of boolean or optional
values, as it is not possible to know the length of the list.
In that case, Eliom always takes the shortest possible list.

===Forms and suffixes

Service with "suffix" URLs have an equivalent version with
usual parameters, allowing creation of forms towards such services.
Example:

<<code language="ocaml"|(* Form for service with suffix: *)
let create_suffixform ((suff, endsuff),i) =
     let module Html = Eliom_content.Html.D in
    <:html5list< <p>Write the suffix:
      $Form.input ~input_type:`Text ~name:suff Form.int$ <br/>
      Write a string: $user_type_input
      (Ocsigen_lib.Url.string_of_url_path ~encode:false)
         ~input_type:`Text ~name:endsuff ()
         $ <br/>
      Write an int: $Form.input ~input_type:`Text ~name:i Form.int$ <br/>
      $Form.input ~input_type:`Submit ~value:"Click" Form.string$</p> ~>>

let suffixform = Eliom_registration.Html.create
  ~path:(Eliom_service.Path ["suffixform"])
  ~meth:(Eliom_service.Get Eliom_parameter.unit)
  (fun () () ->
     let f = Form.get_form isuffix create_suffixform in
     let module Html = Eliom_content.Html.D in
     Lwt.return
      << <html>
           <head><title></title></head>
           <body> $f$ </body>
         </html> ~>>)

>>

===Uploading files

The  <<a_api| val Eliom_parameter.file >> parameter type allows files to be sent in your
request. The service gets something of type
 <<a_api project="ocsigenserver"| type Ocsigen_extensions.file_info >>. You can extract information
using this using these functions (from  <<a_api| module Eliom_request_info >>):

<<code language="ocaml"|val get_tmp_filename : Ocsigen_extensions.file_info -> string
val get_filesize : Ocsigen_extensions.file_info -> int64
val get_original_filename : Ocsigen_extensions.file_info -> string

>>


 <<a_api| val Eliom_request_info.get_tmp_filename >> returns the actual name
of the uploaded file on the hard drive.
 <<a_api| val Eliom_request_info.get_original_filename >> gives the original filename.

To enable file upload, you must configure a directory for uploaded
files in Ocsigen's configuration file.  For example:
<<div class="pre"|
  <uploaddir>/tmp</uploaddir> >>

Files are kept in this directory only while processing the request.
They are automatically removed afterwards. Therefore, your services
must copy the files somewhere else, if the files are to be kept. In
the following example, we create a new hard link to the file to keep
it. (The destination must be on the same partition of the disk.)


<<code language="ocaml"|let upload = Eliom_service.create
    ~path:(Eliom_service.Path ["upload"])
    ~meth:(Eliom_service.Get unit)
    ()

let upload2 = Eliom_registration.Html.register
   ~path:(Eliom_service.Path ["upload"])
   ~meth:(Eliom_service.Post (Eliom_parameter.unit,
                              Eliom_parameter.file "file"))
    (fun () file ->
      let to_display =
        let newname = "/tmp/thefile" in
        (try
          Unix.unlink newname;
        with _ -> ());
        Lwt_log.ign_debug (Eliom_request_info.get_tmp_filename file);
        Unix.link (Eliom_request_info.get_tmp_filename file) newname;
        let fd_in = open_in newname in
        try
          let line = input_line fd_in in close_in fd_in; line (*end*)
        with End_of_file -> close_in fd_in; "vide"
      in
      Lwt.return
        (html
           (head (title (pcdata "Upload")) [])
           (body [h1 [pcdata to_display]])))


let uploadform = Eliom_registration.Html.register upload
    (fun () () ->
      let f =
        (Form.post_form upload2
           (fun file ->
             [p [Form.file_input ~name:file ();
                 br ();
                 Form.input ~input_type:`Submit ~value:"Send" Form.string
               ]]) ()) in
      Lwt.return
        (html
           (head (title (pcdata "form")) [])
           (body [f])))


>>

===Raw POST data (advanced use)

By specifying {{{~post_params:Eliom_parameter.raw_post_params}}},
it is possible to create a service that takes as parameter any POST
data, as a stream. The only restriction is that it does not work if
the content-type corresponds to URL encoded form data or multipart data
(because in these cases, there are POST parameters, which are decoded by Eliom
to find the service).

See the API reference for more information.
